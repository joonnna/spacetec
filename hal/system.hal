newthread fpthread fp 10000000
loadrt muxn count=2
loadrt limit1 count=2
loadrt pid count=2
loadrt bldc cfg=nT,nT #Probably wrong config
loadrt abspos count=2
loadrt steptrack count=2
loadrt sigcheck

# Test input components
loadrt rssi_test
loadrt test_encoder


setp pid.0.enable true
setp pid.1.enable true

# Test values
#setp muxn.0.in0 5.4
#setp muxn.0.in1 3.4
#setp muxn.0.in2 7.4


######################################
#1st set of components and connections
######################################

# Net's for 1st set
net mux-to-lim.0 muxn.0.out limit1.0.in

net lim-to-pid.0 limit1.0.out pid.0.command

net abs-to-pid-step.0 abspos.0.out0 pid.0.feedback steptrack.0.in1

net pid-to-bldc.0 pid.0.output bldc.0.value

net step-to-mux.0 steptrack.0.out muxn.0.in0

net sigcheck-to-step.0 sigcheck.0.out0 steptrack.0.in0


# parameter values
setp limit1.0.max 1
setp limit1.0.min 0
setp pid.0.Pgain 1
setp pid.0.Igain 1

# Remote components
newcomp rmux0
newpin rmux0 rmux0.out0 s32 out
newpin rmux0 rmux0.out1 float out
ready rmux0

newcomp rabspos0
newpin rabspos0 rabspos0.in float in
newpin rabspos0 rabspos0.out float out
ready rabspos0

newcomp rbldc0
newpin rbldc0 rbldc0.in float in
ready rbldc0

net bldc-out0 bldc.0.out rbldc0.in

net sel.0 rmux0.out0 muxn.0.sel
net az rmux0.out1 muxn.0.in1

net oldpos.0 abspos.0.out1 rabspos0.in
net reset.0 rabspos0.out abspos.0.in1


######################################
#2th set of components and connections
######################################

# Net's for 2th set
net lim-to-pid.1 limit1.1.out pid.1.command

net pid-to-bldc.1 pid.1.output bldc.1.value

net abs-to-pid-step.1 abspos.1.out1 pid.1.feedback steptrack.1.in1

net step-to-mux.1 steptrack.1.out muxn.1.in0

net sigcheck-to-step.1 sigcheck.0.out1 steptrack.1.in0

# parameter values
setp limit1.1.max 1
setp limit1.1.min 0
setp pid.1.Pgain 1
setp pid.1.Igain 1

# Remote components
newcomp rmux1
newpin rmux1 rmux1.out0 s32 out
newpin rmux1 rmux1.out1 float out
ready rmux1

newcomp rabspos1
newpin rabspos1 rabspos1.in float in
newpin rabspos1 rabspos1.out float out
ready rabspos1


newcomp rbldc1
newpin rbldc1 rbldc1.in float in
ready rbldc1

net bldc-out1 bldc.1.out rbldc1.in

net sel.1 rmux1.out0 muxn.1.sel
net el rmux1.out1 muxn.1.in1

net oldpos.1 abspos.1.out2 rabspos1.in
net reset.1 rabspos1.out abspos.1.in1


##########################
#Single components
##########################

newcomp rrssi
newpin rrssi rrssi.out float out
ready rrssi

newcomp rsigcheck
newpin rsigcheck rsigcheck.in bit in
ready rsigcheck

net set-rssi rrssi.out rssi-test.0.in

net sig-to-python sigcheck.0.out2 rsigcheck.in
net rssi-to-sig rssi-test.0.out sigcheck.0.in

net enc-to-abs.0 test-encoder.0.out0 abspos.0.in0
net enc-to-abs.1 test-encoder.0.out1 abspos.1.in0


# functions #1
addf test-encoder.0 fpthread
addf rssi-test.0 fpthread
addf sigcheck.0 fpthread

addf abspos.0 fpthread
addf steptrack.0 fpthread
addf muxn.0 fpthread
addf limit1.0 fpthread
addf pid.0.do-pid-calcs fpthread
addf bldc.0 fpthread

addf abspos.1 fpthread
addf steptrack.1 fpthread
addf muxn.1 fpthread
addf limit1.1 fpthread
addf pid.1.do-pid-calcs fpthread
addf bldc.1 fpthread


start

# Haltalk
loadusr haltalk
